\documentclass{article}
\usepackage[utf8]{inputenc}
\usepackage{geometry}
\usepackage{hyperref}
\usepackage{multicol}
\usepackage{graphicx}
\usepackage{float}

\geometry{a4paper, total={170mm,257mm}, left=20mm, top=20mm}
\usepackage{graphicx}
\usepackage{titling}

\title{Group A3D: SPARQL queries and Analytics}
\author{Group A3D}
\date{10th January 2025}

 \usepackage{fancyhdr}
\fancypagestyle{plain}{%  the preset of fancyhdr
    \fancyhf{} % clear all header and footer fields
    % \fancyfoot[R]{\includegraphics[width=2cm]{KULEUVEN_GENT_RGB_LOGO.png}}
    \fancyhead[]{\thedate}
    \fancyhead[L]{SPARQL queries and Analytics}
    \fancyhead[R]{\theauthor}
}
\makeatletter
\def\@maketitle{%
  \newpage
  \null
  \vskip 1em%
  \begin{center}%
  \let \footnote \thanks
    {\LARGE \@title \par}%
    \vskip 1em%
    %{\large \@date}%
  \end{center}%
  \par
  \vskip 1em}
\makeatother

\usepackage{lipsum}
\usepackage{cmbright}

\usepackage{xcolor}
\usepackage{listings}
\lstdefinelanguage{SPARQL}{
    keywords={SELECT, WHERE, AS, FILTER, OPTIONAL, GRAPH, UNION, PREFIX, ORDER, BY, ASC, DESC, LIMIT, OFFSET, BIND, GROUP, HAVING, COUNT, DISTINCT, SUM, AVG, MIN, MAX, GROUP_CONCAT},
    keywordstyle=\color{blue}\bfseries,
    ndkeywords={a, rdf:type},
    ndkeywordstyle=\color{teal}\bfseries,
    identifierstyle=\color{black},
    stringstyle=\color{red}\ttfamily,
    commentstyle=\color{gray}\itshape,
    sensitive=true,
    morecomment=[l][\color{gray}]
}
\lstset{
    language=SPARQL,
    basicstyle=\ttfamily\small,
    numbers=left,
    numberstyle=\tiny\color{gray},
    stepnumber=1,
    frame=single,
    breaklines=true,
    captionpos=b,
    tabsize=2,
    showstringspaces=false
}

\begin{document}

\maketitle

\begin{tabular}{@{}ll}
	\textbf{Group members:}
	 & \href{mailto:andrea.bruttomesso.1@studenti.unipd.it}{Andrea Bruttomesso} 2120933 \\
	 & \href{mailto:alessandro.corro.1@studenti.unipd.it}{Alessandro Corr\`o} 2125034   \\
	 & \href{mailto:davide.seghetto@studenti.unipd.it}{Davide Seghetto} 2122548         \\
	 & \href{mailto:andrea.stocco.8@studenti.unipd.it}{Andrea Stocco} 2108885           \\
\end{tabular}


\section*{TASK}
Provide at least 8 SPARQL queries over your RDF datasets. You may also perform advanced data analytics to uncover interesting insights from your datasets. Please submit a PDF that includes the SPARQL queries along with relevant plots or tables summarizing your analytics. For each query, provide a description that explains its purpose and overall objective.

\subsection*{Query 1 - papersNobelTopicsYear}
This query shows the topics present in both Nobel motivations and paper abstracts.
For a given year, it returns the number of paper in which these topics appear.
This query can be used to find correlations between Nobel Prize topics and research
papers.

For instance, running this query for the year 2004 shows that the topic "protein"
appeared in 28 papers. Hence, we could say that in 2004 chemistry was the main
research field (considering the limited number of papers available) and that the
main topic of the Nobel Prize awarded in that year was studied by several
researcher.

Unfortunately, this query is not always useful. In some cases, the
main topics may include words like "method" and "analysis", which are not
informative enough to determine how extensively a specific topic was studied
in a given year.

Due to the distribution of research papers in our dataset across different years,
this query provides more meaningful results for years after 2000.

\begin{lstlisting}
PREFIX spif: <http://spinrdf.org/spif#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX jur: <http://sweet.jpl.nasa.gov/2.3/humanJurisdiction.owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX : <http://www.semanticweb.org/a3d/ontologies/2024/10/nobelOntology/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?singleAbTopic ?nobel (COUNT(?paper) AS ?numPapers) WHERE {
    {
        SELECT ?singleAbTopic ?paper WHERE {
        		?paper :hasAbstractTopics ?topics;
                :hasYear "2004"^^xsd:gYear.
        		?singleAbTopic spif:split(?topics ",")
        }
    }
    {
        SELECT ?singleNoTopic ?nobel WHERE {
            ?nobel :hasMotivationTopics ?topics;
                :hasYear "2004"^^xsd:gYear.
            ?singleNoTopic spif:split(?topics ",")
        }
    }
    FILTER (?singleAbTopic = ?singleNoTopic)
}
GROUP BY ?singleAbTopic ?nobel
ORDER BY desc(?numPapers)
\end{lstlisting}

\subsection*{Query 2 - papersPerTopic}

\begin{lstlisting}
PREFIX spif: <http://spinrdf.org/spif#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX jur: <http://sweet.jpl.nasa.gov/2.3/humanJurisdiction.owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX : <http://www.semanticweb.org/a3d/ontologies/2024/10/nobelOntology/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?singleTopic (COUNT(?paper) AS ?numPapers) WHERE {
    ?paper :hasAbstractTopics ?topics.
    ?singleTopic spif:split(?topics ",")
}
GROUP BY ?singleTopic
ORDER BY desc(?numPapers)    
\end{lstlisting}

Papers per topic

\subsection*{Query 3 - sharedNobels}
This query shows the number of Nobel Prizes shared by multiple laureates
and the number of laureates sharing Nobel Prizes.

\noindent The query provides an interesting result: 242 out of 579 Nobel Prizes (41.8\%) have
been shared by multiple laureates, and 632 laureates have shared different Nobel Prizes.
On average, a Nobel Prize is shared by more than two laureates (2.2 laureates per prize).

\begin{lstlisting}
PREFIX : <http://www.semanticweb.org/a3d/ontologies/2024/10/nobelOntology/>

SELECT ?share (COUNT(?nobel) AS ?numSharedNobels) (SUM(?share) AS                    ?numLaureatesSharingNobels) WHERE { %lasciare lo spazio dopo AS
    ?nobel :hasPrizeShare ?share.
} GROUP BY ?share
\end{lstlisting}

\noindent For each value in the x axis, representing the number of people sharing a certain Nobel Prize, the following chart displays two bars. 
The first bar represents the total number of Nobel Prizes shared among the specified number of laureates.
The second bar represents the total number of laureates who have shared these Nobel Prizes.
\begin{figure}[H]
    \centering
    \includegraphics[width=0.7\linewidth]{../queries/plots/prizeShare.png}
    \caption{Graph showing the distribution of Nobel Prizes and laureates.}
    \label{fig:prizeShare}
\end{figure}

\subsection*{Query 4 - laureatesCollaborations}

\begin{lstlisting}
PREFIX : <http://www.semanticweb.org/a3d/ontologies/2024/10/nobelOntology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

SELECT ?title (GROUP_CONCAT(?name; separator=", ") AS ?laureates) WHERE {
    ?laureate rdf:type :Laureate .
    ?paper rdf:type :Paper ;
        :hasTitle ?title . 
    ?laureate :hasWritten ?paper .
    ?laureate foaf:name ?name .
}
GROUP BY ?title
HAVING (COUNT(DISTINCT ?laureate) > 1)    
\end{lstlisting}

Laureates that wrote a paper together

\subsection*{Query 5 - How fundings in R\&D affect the possibility for a country to win a Nobel?}

\begin{lstlisting}
PREFIX : <http://www.semanticweb.org/a3d/ontologies/2024/10/nobelOntology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?year ?topCountry (COUNT(DISTINCT ?laureate) AS ?numLaureates) (SUM(?fundingAmount) AS ?totalFunding) WHERE {
  ?laureate rdf:type :Laureate ;
  			   :hasWon ?nobelPrize ;
      		   :bornIn ?city .
  ?nobelPrize :hasYear ?year .
  ?city :locatedIn ?topCountry .

  OPTIONAL {
    ?topCountry :hasFunded ?funding .
    ?funding :hasYear ?year ;
     		 :hasAmount ?fundingAmount .
  } 
  { # Select country with most laureates
    SELECT (?country AS ?topCountry) WHERE {
      ?laureate rdf:type :Laureate ;
       			   :bornIn ?city .
      ?city :locatedIn ?country .
    }
    GROUP BY ?country
    ORDER BY DESC(COUNT(DISTINCT ?laureate))
    LIMIT 3
  }
}
GROUP BY ?year ?topCountry
HAVING(SUM(?fundingAmount) > 0)
ORDER BY ?year ?topCountry   
\end{lstlisting}

\vspace{1em}

With this query, we identified the top three countries with the highest number of Nobel laureates born there, along with the annual amount of funding allocated to research and development (R\&D) by these nations. To ensure data consistency, we focused exclusively on the years from 2000 to 2016.

\begin{figure}[h!]
    \centering
    \includegraphics[width=0.9\textwidth]{../queries/plots/funding_comparison_by_country.png}
    \caption{Funding Comparison by Country}
\end{figure}

\begin{figure}[h!]
    \centering
    \includegraphics[width=0.9\textwidth]{../queries/plots/laureates_comparison_by_country.png}
    \caption{Laureates Comparison by Country}
\end{figure}

The graphs reveal a strong correlation between R\&D funding and the number of Nobel laureates. In particular, the United States dominates both metrics, demonstrating how substantial investments in research directly contribute to significant achievements in this field, resulting in a higher number of laureates annually.

The situation in Great Britain, highlighted in the following plot, is particularly curious and further supports this observation:

\begin{figure}[h!]
    \centering
    \includegraphics[width=0.9\textwidth]{../queries/plots/gb_funding_trend_bar.png}
    \caption{Great Britain R\&D Funding Trend}
\end{figure}

It's clear that the trends in funding and the number of laureates mirror each other closely. From 2001 to 2003, we observe the same pattern in both metrics. Subsequently, a steady and low level of R\&D funding still reflects the number of British Nobel laureates until 2016, when a sharp increase in Nobel prizes matches with a significant rise in R\&D investments.

\subsection*{Query 6 - moreThanOneNobel}

\begin{lstlisting}
PREFIX spif: <http://spinrdf.org/spif#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX jur: <http://sweet.jpl.nasa.gov/2.3/humanJurisdiction.owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX : <http://www.semanticweb.org/a3d/ontologies/2024/10/nobelOntology/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?laureate (COUNT(?nobel) AS ?numNobels) WHERE {
    ?laureate :hasWon ?nobel.
}
GROUP BY ?laureate
HAVING (?numNobels > 1)    
\end{lstlisting}

who won more than one nobel prize

\subsection*{Query 7 - papersPerVenue}
This plot shows the number of papers published over the years by major
venues (those with at least 800 papers published, according to our dataset).

In recent years, Bioinformatics could be considered one of the most influential
venue due to its consistently higher number of papers published compared to others.
IEEE venues, are the most prominent in the fields of information and tecnology.

For instance, on 2009, the research community focused more on the field of
communications.
That same year, the Physics Nobel Prize was awarded for "groundbreaking achievements
concerning the transmission of light in fibers for optical communication".

\begin{lstlisting}
PREFIX spif: <http://spinrdf.org/spif#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX jur: <http://sweet.jpl.nasa.gov/2.3/humanJurisdiction.owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX : <http://www.semanticweb.org/a3d/ontologies/2024/10/nobelOntology/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?venue ?year (COUNT(?paper) AS ?numPapers) WHERE {

    # get the most important venues (the ones with at least 800 papers published)
    {
        SELECT ?venue (COUNT(?paper) AS ?totPapers) WHERE {
            ?paper :publishedIn ?venue.
        }
        GROUP BY ?venue
        HAVING (?totPapers > 800)
        ORDER BY DESC (?totPapers)
    }

    # get the number of paper published in the most important venues for each year
    ?paper :publishedIn ?venue;
        :hasYear ?year.
}
GROUP BY ?venue ?year
ORDER BY ASC (?year)    
\end{lstlisting}

\subsection*{Query 8 - papersPerCategory}
The following query allows us to extract, for each year, the number of scientific articles published in each relevant category. The categories returned
as results are the TopConcepts categories of our SKOS taxonomy, and they include in the count their various subcategories. For example, in the count
of papers for the medicine category, articles belonging to subcategories like neuroscience are also included.

\noindent To obtain this data, the query uses two distinct subqueries. The first subquery extracts the number of articles published for each main category
(TopConcept), while the second identifies the number of articles associated with the subcategories of each main category. The sum of the results of
the two subqueries, aggregated by year and category, provides the total number of articles published for each category and for each year.

\begin{lstlisting}
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX : <http://www.semanticweb.org/a3d/ontologies/2024/10/nobelOntology/>
# Extracts the number of papers we have for each category over the years --> the most studied research areas over the years
SELECT ?year ?category (SUM(?howmany) AS ?totalPapers) WHERE {
    # Inner query to extract the number of papers published in journals that have at least one category that is a top concept of our skos scheme
    {
        SELECT ?year ?category (COUNT(DISTINCT ?paper) AS ?howmany) WHERE {
            ?journal :hasJournalCategory ?category .
            :journalCategoryScheme skos:hasTopConcept ?category .
            ?paper :publishedIn ?journal ;
                :hasYear ?year .
        } 
        GROUP BY ?year ?category
    }
    UNION
    # Inner query to extract the number of papers published in journals that have at least one category that is a subcategory of a top concept category
    {
        SELECT ?year ?category (COUNT(DISTINCT ?paper) AS ?howmany) WHERE {
            ?journal :hasJournalCategory ?cat .
            ?cat skos:broaderTransitive ?category .
            ?paper :publishedIn ?journal ;
                :hasYear ?year .
        } 
        GROUP BY ?year ?category
    }
} 
GROUP BY ?year ?category
ORDER BY DESC (?totalPapers)    
\end{lstlisting}

\newpage

\noindent This approach offers a comprehensive view of the distribution of published articles over time, allowing us to identify which research areas, related
to Nobel categories, have attracted the most attention from scholars over the years.

\noindent Below is a plot representing the trend of the number of papers published over the years, divided by their respective categories.
In the recent years, the most studied field is medicine which got a big leap around 2002, while in the nineties the most studied one was economics, which
reached its peak in 2008, probability due to the economic crisis of that time.
\begin{figure}[ht]
	\centering
	\includegraphics[width=\textwidth]{../queries/plots/papersPerCategory.png}
\end{figure}

\end{document}
